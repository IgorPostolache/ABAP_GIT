*&---------------------------------------------------------------------*
*& Module Pool       ZIP_P_TEST_000
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

PROGRAM zip_p_test_000.

DATA: ok_code            LIKE sy-ucomm,
      save_ok            LIKE sy-ucomm,

      g_container        TYPE scrfname VALUE 'BCALV_GRID_DEMO_0100_CONT1',
      g_custom_container TYPE REF TO cl_gui_custom_container,
      g_grid             TYPE REF TO cl_gui_alv_grid,
      gs_layout          TYPE lvc_s_layo.

DATA: BEGIN OF gt_outtab OCCURS 0.
        INCLUDE STRUCTURE zip_sflight.
DATA: celltab TYPE lvc_t_styl.
DATA: END OF gt_outtab.

CLASS lcl_report DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS:
      check_if_rows_deleted
        IMPORTING
            er_data_changed TYPE REF TO cl_alv_changed_data_protocol,
      check_if_rows_changed
        IMPORTING
            er_data_changed TYPE REF TO cl_alv_changed_data_protocol,
      exit_program,
      switch_edit_mode,
      display_grid,
      fill_celltab,
      save.

    METHODS:
        get_data,
        handle_chaged_data
            FOR EVENT data_changed OF cl_gui_alv_grid
                IMPORTING er_data_changed.

  PRIVATE SECTION.
    CLASS-DATA:
      lt_pasajeros TYPE TABLE OF zip_sflight,
      lt_celltab   TYPE lvc_t_styl,
      l_index      TYPE i,
      ls_styletab  TYPE lvc_t_styl,
      delete_oper  TYPE c,
      update_oper  TYPE c.

ENDCLASS.

CLASS lcl_report IMPLEMENTATION.

  METHOD check_if_rows_changed.
    DESCRIBE TABLE er_data_changed->mt_good_cells LINES DATA(lv_num_changed_rows).

    IF lv_num_changed_rows > 0.
      DATA:
            dl_ins_row TYPE lvc_s_moce,
            lt_aux_pasajeros TYPE TABLE OF zip_sflight.

      LOOP AT er_data_changed->mt_good_cells ASSIGNING FIELD-SYMBOL(<row>).

        IF <row>-error = 'X' OR <row>-value = ''.
          EXIT.
        ENDIF.

      ENDLOOP.

      update_oper = 'X'.

    ENDIF.

  ENDMETHOD.

  METHOD check_if_rows_deleted.

    DESCRIBE TABLE er_data_changed->mt_deleted_rows LINES DATA(lv_num_deleted_rows).

    IF lv_num_deleted_rows > 0.
      DATA:
            dl_ins_row TYPE lvc_s_moce,
            lt_aux_pasajeros TYPE TABLE OF zip_sflight.

      LOOP AT er_data_changed->mt_deleted_rows ASSIGNING FIELD-SYMBOL(<row>).
        READ TABLE lt_pasajeros ASSIGNING FIELD-SYMBOL(<ls_pasajeros>) INDEX <row>-row_id.
        IF sy-subrc EQ 0.
          APPEND <ls_pasajeros> TO lt_aux_pasajeros.
        ENDIF.
      ENDLOOP.

      IF lines( lt_aux_pasajeros ) > 0.

        DELETE zip_sflight FROM TABLE lt_aux_pasajeros.

        IF sy-subrc EQ 0.
          delete_oper = 'X'.
          switch_edit_mode(  ).
          MESSAGE `Selected rows were deleted.` TYPE 'I'.
        ELSE.
          switch_edit_mode(  ).
          MESSAGE `An error occurred while tryig to delete the selected rows.` TYPE 'E' DISPLAY LIKE 'I'.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDMETHOD.

  METHOD handle_chaged_data.

    check_if_rows_deleted( er_data_changed ).
    check_if_rows_changed( er_data_changed ).

  ENDMETHOD.

  METHOD fill_celltab.

    DATA:
        ls_stylerow TYPE lvc_s_styl,
        l_mode      TYPE raw4.

    LOOP AT gt_outtab ASSIGNING FIELD-SYMBOL(<gs_outtab>).

      REFRESH <gs_outtab>-celltab.

      REFRESH ls_styletab.
      ls_stylerow-fieldname = 'DNI'.
      ls_stylerow-style = cl_gui_alv_grid=>mc_style_disabled.
      APPEND ls_stylerow TO ls_styletab.

      INSERT LINES OF ls_styletab INTO TABLE <gs_outtab>-celltab.
      MODIFY gt_outtab FROM <gs_outtab>.

      REFRESH ls_styletab.
      ls_stylerow-fieldname = 'AGE'.
      ls_stylerow-style = cl_gui_alv_grid=>mc_style_enabled.
      APPEND ls_stylerow TO ls_styletab.

      INSERT LINES OF ls_styletab INTO TABLE <gs_outtab>-celltab.
      MODIFY gt_outtab FROM <gs_outtab>.

    ENDLOOP.

  ENDMETHOD.

  METHOD get_data.
    DATA: gs_outtab LIKE LINE OF gt_outtab.

    SELECT * FROM zip_sflight INTO TABLE lt_pasajeros.

    LOOP AT lt_pasajeros ASSIGNING FIELD-SYMBOL(<ls_pasajeros>).
      MOVE-CORRESPONDING <ls_pasajeros> TO gs_outtab.
      APPEND gs_outtab TO gt_outtab.
    ENDLOOP.

  ENDMETHOD.

  METHOD exit_program.
    LEAVE PROGRAM.
  ENDMETHOD.

  METHOD switch_edit_mode.

    IF g_grid->is_ready_for_input(  ) EQ 0.

      CALL METHOD g_grid->set_ready_for_input
        EXPORTING
          i_ready_for_input = 1.

    ELSE.
      CALL METHOD g_grid->set_ready_for_input
        EXPORTING
          i_ready_for_input = 0.
    ENDIF.

  ENDMETHOD.

  METHOD display_grid.

    gs_layout-stylefname = 'CELLTAB'.
    gs_layout-edit = 'X'.
    fill_celltab(  ).

    CALL METHOD g_grid->set_ready_for_input
      EXPORTING
        i_ready_for_input = 0.


    CALL METHOD g_grid->set_table_for_first_display
      EXPORTING
        i_structure_name = 'ZIP_SFLIGHT'
        is_layout        = gs_layout
      CHANGING
        it_outtab        = gt_outtab[].

  ENDMETHOD.

  METHOD save.

    IF g_grid IS INITIAL.
      CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
        IMPORTING
          e_grid = g_grid.
    ENDIF.

    IF g_grid IS NOT INITIAL.

      DATA:
            v_edited TYPE char1.

      CALL METHOD g_grid->check_changed_data
        IMPORTING
          e_valid = v_edited.

      IF v_edited = 'X' AND delete_oper IS INITIAL AND update_oper = 'X'.

        DATA:
                lt_aux_pasajeros TYPE TABLE OF zip_sflight,
                ls_aux_pasajeros LIKE LINE OF lt_aux_pasajeros.

        LOOP AT gt_outtab ASSIGNING FIELD-SYMBOL(<gs_outtab>).
          MOVE-CORRESPONDING <gs_outtab> TO ls_aux_pasajeros .
          APPEND ls_aux_pasajeros TO lt_aux_pasajeros.
        ENDLOOP.

        MODIFY zip_sflight FROM TABLE lt_aux_pasajeros .

        IF sy-subrc EQ 0.
          switch_edit_mode(  ).
          MESSAGE `DB successefull updated.` TYPE 'I'.
        ELSE.
          switch_edit_mode(  ).
          MESSAGE `DB not updated.` TYPE 'E' DISPLAY LIKE 'I'.
        ENDIF.

        update_oper = ''.

      ENDIF.
    ENDIF.

  ENDMETHOD.
ENDCLASS.


MODULE pbo OUTPUT.
  SET PF-STATUS 'MAIN100'.
  SET TITLEBAR 'MAIN100'.

  IF g_custom_container IS INITIAL.
    CREATE OBJECT g_custom_container
      EXPORTING
        container_name = g_container.

    CREATE OBJECT g_grid
      EXPORTING
        i_parent = g_custom_container.

    DATA(lo_report) = NEW lcl_report(  ).
    SET HANDLER lo_report->handle_chaged_data FOR g_grid.

    lo_report->get_data(  ).
    lo_report->display_grid(  ).

  ENDIF.
ENDMODULE.

MODULE pai INPUT.
  save_ok = ok_code.
  CLEAR ok_code.

  CASE save_ok.
    WHEN 'EXIT'.
      lcl_report=>exit_program(  ).
    WHEN 'SAVE'.
      lcl_report=>save(  ).
    WHEN 'SWITCH'.
      lcl_report=>switch_edit_mode(  ).
  ENDCASE.
ENDMODULE.